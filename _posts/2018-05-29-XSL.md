---
layout: post
title: 'XSL'
date: 2018-05-29
author: HenryHX
cover: '/assets/img/2018-05-29-XSL/XML-XSLT.jpg'
tags: XSL XML XSLT
---

# XSL

>XSL 指扩展样式表语言（EXtensible Stylesheet Language）。
万维网联盟开始发展 XSL 的起因是由于对基于 XML 的样式表语言的需求。
XSLT 指 XSL 转换。在此教程中，你将学习如何++使用 XSLT 将 XML 文档转换为其他文档++，比如 XHTML。

[XPath_a]:#XPath_a
[XPath_lang_a]:#XPath_lang_a
[XPath_opera_a]:#XPath_opera_a

[xsl_lang_a]:#xsl_lang_a
[xsl_value_of_a]:#xsl_value_of_a
[xsl_variable_a]:#xsl_variable_a
[xsl_symbol_a]:#xsl_symbol_a
[xsl_if_a]:#xsl_if_a
[xsl_for_each_a]:#xsl_for_each_a
[xsl_choose_a]:#xsl_choose_a
[xsl_template_a]:#xsl_template_a
[xsl_sort_a]:#xsl_sort_a
[xsl_apply_templates_a]:#xsl_apply_templates_a
[xsl_message_a]:#xsl_message_a
[xsl_others_a]:#xsl_others_a
[xsl_transform_a]:#xsl_transform_a

## 目录

- [XPath][XPath_a]
    - [XPath语法][XPath_lang_a]
    - [XPath运算符][XPath_opera_a]
- [XSL语法][xsl_lang_a]
    - [xsl:value-of][xsl_value_of_a]
    - [xsl:variable][xsl_variable_a]
    - [@和＄符号][xsl_symbol_a]
    - [xsl:if][xsl_if_a]
    - [xsl:for-each][xsl_for_each_a]
    - [xsl:choose xsl:when][xsl_choose_a]
    - [xsl:template][xsl_template_a]
    - [xsl:sort][xsl_sort_a]
    - [xsl:apply-templates][xsl_apply_templates_a]
    - [xsl:message][xsl_message_a]
    - [其他元素][xsl_others_a]
- [转换][xsl_transform_a]


## <a id="XPath_a">XPath</a>
XPath 是一门在 XML 文档中查找信息的语言。XPath 可用来在 XML 文档中对元素和属性进行遍历。
XPath 是 W3C XSLT 标准的主要元素，并且 XQuery 和 XPointer 都构建于 XPath 表达之上。

#### 1. <a id="XPath_lang_a">XPath语法</a>
XPath 使用路径表达式来选取 XML 文档中的节点或节点集。节点是通过沿着路径 (path) 或者步 (steps) 来选取的。

表达式|描述
---|---
nodename|选取此节点的所有子节点。
/|从根节点选取。
//|从匹配选择的当前节点选择文档中的节点，而不考虑它们的位置。
.|选取当前节点。
..|选取当前节点的父节点。
@|选取属性。

**实例**
路径表达式|结果
---|---
/bookstore/book[1]|选取属于 bookstore 子元素的第一个 book 元素。
/bookstore/book[last()]|选取属于 bookstore 子元素的最后一个 book 元素。
/bookstore/book[last()-1]|选取属于 bookstore 子元素的倒数第二个 book 元素。
/bookstore/book[position()<3]|选取最前面的两个属于 bookstore 元素的子元素的 book 元素。
//title[@lang]|选取所有拥有名为 lang 的属性的 title 元素。
//title[@lang='eng']|选取所有 title 元素，且这些元素拥有值为 eng 的 lang 属性。
/bookstore/book[price>35.00]|选取 bookstore 元素的所有 book 元素，且其中的 price 元素的值须大于 35.00。
/bookstore/book[price>35.00]/title|选取 bookstore 元素中的 book 元素的所有 title 元素，且其中的 price 元素的值须大于 35.00。

通配符|描述
---|---
*|匹配任何元素节点。
@*|匹配任何属性节点。
node()	匹配任何类型的节点。

**实例**

路径表达式|结果
---|---
/bookstore/*|选取 bookstore 元素的所有子元素。
//*|选取文档中的所有元素。
//title[@*]|选取所有带有属性的 title 元素。

["点击查看实例"]("http://www.w3school.com.cn/xpath/xpath_syntax.asp")

#### 2. <a id="XPath_opera_a">XPath运算符</a>
XPath 表达式可返回节点集、字符串、逻辑值以及数字。

运算符|描述|实例|返回值
---|---|---|---
+|加法|6 + 4|10
-|减法|6 - 4|2
*|乘法|6 * 4|24
div|除法|8 div 4|2
=|等于|	price=9.80|如果price 是 9.80，则返回 true。如果price 是 9.90，则返回 false。
!=|不等于|price!=9.80	|如果 price 是 9.90，则返回 true。如果 price 是 9.80，则返回 false。
<|小于|price<9.80	|如果 price 是 9.00，则返回 true。如果 price 是 9.90，则返回 false。
<=|小于或等于|price<=9.80	|如果 price 是 9.00，则返回 true。如果 price 是 9.90，则返回 false。
>|大于|price>9.80	|如果 price 是 9.90，则返回 true。如果 price 是 9.80，则返回 false。
>=|大于或等于|price>=9.80|如果 price 是 9.90，则返回 true。如果 price 是 9.70，则返回 false。
or|或|price=9.80 or price=9.70|如果 price 是 9.80，则返回 true。如果 price 是 9.50，则返回 false。
and|与|price>9.00 and price<9.90|如果 price 是 9.80，则返回 true。如果 price 是 8.50，则返回 false。
mod|计算除法的余数|5 mod 2|1

## <a id="xsl_lang_a">XSL语法</a>

#### 1. <a id="xsl_value_of_a">xsl:value-of</a>
`<xsl:value-of>`元素用于提取某个选定节点的值，并把值添加到转换的输出流中：

```
<xsl:value-of select="name" /> //匹配当前节点的所有子节点为<name>的元素.
<xsl:value-of select="//student" />// 匹配当前节点中所有后继节点名为<student>的元素.
```

#### 2. <a id="xsl_variable_a">xsl:variable</a>
`<xsl:variable>`元素用于声明局部或全局的变量,调用变量使用`$变量名称`

- 如果设置了`select`属性，`<xsl:variable>` 元素就不能包含任何内容。如果 `select` 属性含有文字字符串，则必须给字符串加引号。下面的两个例子为变量 "color" 赋值 "red"：
```
<xsl:variable name="color" select="'red'" />
<xsl:variable name="color" select='"red"' />
```

- 如果 `<xsl:variable>` 元素只包含 `name` 属性，且没有内容，则变量的值是空字符串：
```
<xsl:variable name="j" />
<xsl:variable name="lowercase" select="'abcdefghijklmnopqrstuvwxyz'"/>
<xsl:variable name="uppercase" select="'ABCDEFGHIJKLMNOPQRSTUVWXYZ'"/>
<xsl:variable name="temp" select="@name"/>
<xsl:value-of select="$temp"/>
<xsl:value-of select="concat(translate(substring(@name,1,1), $uppercase, $lowercase), substring(@name,2))"/>
```

#### 3. <a id="xsl_symbol_a">@和＄符号</a>
$表示变量，@表示节点的属性。

```
//如果发现有<name>元素，则将<name>元素的值输出。
<xsl:if test="@name"> 
    <p><xsl:value-of select="@name"/></p>
</xsl:if>
```

#### 4. <a id="xsl_if_a">xsl:if</a>
`<xsl:if>` 元素用于放置针对 XML 文件内容的条件测试。

```
//必选的 test 属性的值包含了需要求值的表达式。
<xsl:if test="expression">
  ...
  ...如果条件成立则输出...
  ...
</xsl:if>

<xsl:if test="price &gt; 10">
<xsl:if test="position()=last()">
<xsl:if test="@type='String'">
```

#### 5. <a id="xsl_for_each_a">xsl:for-each</a>
`<xsl:for-each>` 元素允许您在 XSLT 中进行循环。

通过在 `<xsl:for-each>` 元素中添加一个选择属性的判别式，我们也可以过滤从 XML 文件输出的结果。

```
<xsl:for-each select="catalog/cd[artist='Bob Dylan']">
```
合法的过滤运算符:
- =  (等于)
- != (不等于)
- &lt; (小于)
- &gt; (大于)

```
//select 属性的值是一个 XPath 表达式。此表达式的工作方式类似于定位某个文件系统，在其中正斜杠可选择子目录。
//查找catalog下所有cd元素
<xsl:for-each select="catalog/cd">
    <tr>
      <td><xsl:value-of select="title"/></td>
      <td><xsl:value-of select="artist"/></td>
    </tr>
</xsl:for-each>

//查找每一个Field元素，然后在/EntityModel/Fields/Field中查找Name匹配的最后一个元素
<xsl:for-each select="Field">
  <xsl:variable name="name" select="@name" />
  <xsl:for-each select="/EntityModel/Fields/Field[@name=$name]">
    <xsl:if test="position()=last()">
      this.<xsl:value-of select="@name" /> = tf<xsl:value-of select="$temp" />Field.<xsl:value-of select="@name" />;<no_new_line />
    </xsl:if>
  </xsl:for-each>
</xsl:for-each>
```

#### 6. <a id="xsl_choose_a">xsl:choose xsl:when</a>
`<xsl:choose>` 元素用于结合 `<xsl:when>` 和 `<xsl:otherwise>` 来表达多重条件测试。

```
<xsl:choose>
  <xsl:when test="expression">
    ... 输出 ...
  </xsl:when>
  <xsl:otherwise>
    ... 输出 ....
  </xsl:otherwise>
</xsl:choose>

<xsl:choose>
  <xsl:when test="@type='String'">
    ///<xsl:value-of select="@label" />
    [DataMember(Name = "<xsl:value-of select="@name" />")]
    public string <xsl:value-of select="@name" />
    {
    get { return _<xsl:value-of select="concat(translate(substring(@name,1,1), $uppercase, $lowercase), substring(@name,2))" />; }
    set
    {
    if (_<xsl:value-of select="concat(translate(substring(@name,1,1), $uppercase, $lowercase), substring(@name,2))" /> != value)
    {
    _<xsl:value-of select="concat(translate(substring(@name,1,1), $uppercase, $lowercase), substring(@name,2))" /> = value;
    NotifyPropertyChange("<xsl:value-of select="@name" />");
    }
    }
    }
    private string _<xsl:value-of select="concat(translate(substring(@name,1,1), $uppercase, $lowercase), substring(@name,2))" />;
  </xsl:when>
  <xsl:when test="@type='Int'">
    ///<xsl:value-of select="@label" />
    [DataMember(Name = "<xsl:value-of select="@name" />")]
    public Int32 <xsl:value-of select="@name" />
  </xsl:when>
  <xsl:when test="@type='Double'">
    ///<xsl:value-of select="@label" />
    [DataMember(Name = "<xsl:value-of select="@name" />")]
    public double <xsl:value-of select="@name" />
  </xsl:when>
  <xsl:when test="@type='Date'">
    ///<xsl:value-of select="@label" />
    [DataMember(Name = "<xsl:value-of select="@name" />")]
    public string <xsl:value-of select="@name" />
  </xsl:when>
  <xsl:when test="@type='Time'">
    ///<xsl:value-of select="@label" />
    [DataMember(Name = "<xsl:value-of select="@name" />")]
    public string <xsl:value-of select="@name" />
  </xsl:when>
  <xsl:when test="@type='Bool'">
    ///<xsl:value-of select="@label" />
    [DataMember(Name = "<xsl:value-of select="@name" />")]
    public byte <xsl:value-of select="@name" />
  </xsl:when>
  <xsl:when test="@type='EnumChar'">
    ///<xsl:value-of select="@label" />
    [DataMember(Name = "<xsl:value-of select="@name" />")]
    public char <xsl:value-of select="@name" />
  </xsl:when>
  <xsl:otherwise>
    <xsl:message>
      TYPE DOES NOT EXIST![<xsl:value-of select="@type" />]
    </xsl:message>
  </xsl:otherwise>
</xsl:choose>
```

#### 7. <a id="xsl_template_a">xsl:template</a>
XSL 样式表由一个或多套被称为模板（template）的规则组成。
每个模板含有当某个指定的节点被匹配时所应用的规则。

`<xsl:template>` 元素用于构建模板。
`match` 属性用于关联 XML 元素和模板。match 属性也可用来为整个文档定义模板。match 属性的值是 XPath 表达式（举例，match="/" 定义整个文档）。

```
<?xml version="1.0" encoding="UTF-8"?>
<xsl:stylesheet version="1.0"
xmlns:xsl="http://www.w3.org/1999/XSL/Transform">
<xsl:template match="/">
  <html>
  <body>
    <h2>My CD Collection</h2>
    <table border="1">
      <tr bgcolor="#9acd32">
        <th>Title</th>
        <th>Artist</th>
      </tr>
      <xsl:for-each select="catalog/cd[artist='Bob Dylan']">
        <tr>
          <td><xsl:value-of select="title"/></td>
          <td><xsl:value-of select="artist"/></td>
        </tr>
      </xsl:for-each>
    </table>
  </body>
  </html>
</xsl:template>
</xsl:stylesheet>
```
++实例解释：++
由于 XSL 样式表本身也是一个 XML 文档，因此它总是由 XML 声明起始：`<?xml version="1.0" encoding="ISO-8859-1"?>`.
下一个元素，`<xsl:stylesheet>`， ，定义此文档是一个 XSLT 样式表文档（连同版本号和 XSLT 命名空间属性）。
`<xsl:template>` 元素定义了一个模板。而 match="/" 属性则把此模板与 XML 源文档的根相联系。
`<xsl:template>` 元素内部的内容定义了写到输出结果的 HTML 代码。
最后两行定义了模板的结尾及样式表的结尾。
这个实例的结果有一点小缺陷，因为数据没有从 XML 文档被复制到输出。在下一章中，您将学习到如何使用 `<xsl:value-of>` 元素从 XML 元素选取值。

#### 8. <a id="xsl_sort_a">xsl:sort</a>
`<xsl:sort>` 元素用于对结果进行排序。

++在何处放置排序信息:++
如需对结果进行排序，只要简单地在 XSL 文件中的 `<xsl:for-each>` 元素内部添加一个 `<xsl:sort>` 元素：

属性|值|描述
---|---|---
select|XPath-expression|	可选。规定节点的排序关键字，即根据哪个节点或节点集来排序。
lang|language-code|可选。规定排序所用的语言。
data-type|text/number/qname|可选。规定被排序的数据的数据类型。默认是 "text"。
order|ascending/descending|可选。规定排序顺序。默认是 "ascending"。
case-order|upper-first/lower-first|可选。规定是首先按大写字母顺序还是小写字母顺序进行排序。


```
//select 属性指示需要排序的 XML 元素。
<xsl:for-each select="Field">
  <xsl:sort select="@formIndex" data-type="number" order="ascending" />
  <xsl:if test="@formIndex!=''">
    &lt;Label Height="28" FontSize="13" FontFamily="微软雅黑" FontWeight="Light" Margin="1" Content="<xsl:value-of select="@label"/>:" /&gt;<no-new-line/>
  </xsl:if>
</xsl:for-each>
```

#### 9. <a id="xsl_apply_templates_a">xsl:apply-templates</a>
`<xsl:apply-templates>` 元素可把一个模板应用于当前的元素或者当前元素的子节点。

假如我们向 `<xsl:apply-templates>` 元素添加一个 `select` 属性，此元素就会仅仅处理与属性值匹配的子元素。我们可以使用 `select` 属性来规定子节点被处理的顺序。
请看下面的 XSL 样式表：
["点击查看测试结果"](http://www.runoob.com/try/tryxslt.php?xmlfile=cdcatalog&xsltfile=cdcatalog_apply)

```
<xsl:template match="/">
  <html>
  <body>
  <h2>My CD Collection</h2>  
  <xsl:apply-templates/>  
  </body>
  </html>
</xsl:template>

<xsl:template match="cd">
  <p>
    <xsl:apply-templates select="title"/>  
    <xsl:apply-templates select="artist"/>
  </p>
</xsl:template>

<xsl:template match="title">
  Title: <span style="color:#ff0000">
  <xsl:value-of select="."/></span>
  <br />
</xsl:template>

<xsl:template match="artist">
  Artist: <span style="color:#00ff00">
  <xsl:value-of select="."/></span>
  <br />
</xsl:template>
```

#### 10. <a id="xsl_message_a">xsl:message</a>
`<xsl:message>` 元素向输出写一条消息。该元素主要用于报告错误。该元素能够包含几乎任何其他的 XSL 元素（`<xsl:text>` 、`<xsl:value-of>` 等等）。
`terminate` 属性允许您选择在错误发生时，是退出处理还是继续处理。

属性|值|描述
terminate|yes/no|可选。"yes"：在消息写入输出后，终止处理。"no"：在消息写入输出后，继续进行处理。默认是 "no"。

```
<xsl:message terminate="yes|no">
  <!-- Content:template -->
</xsl:message>
```

#### 11. <a id="xsl_others_a">其他元素</a>
[点击查看"完整的 XSLT 元素参考手册"]("http://www.runoob.com/xsl/xsl-w3celementref.html")

## <a id="xsl_editor_a">编辑器</a>
XML 编辑器应该具有如下能力：
- 为开始标签自动添加结束标签
- 强制您编写合法的 XML
- 根据 DTD 来验证 XML
- 根据 Schema 来验证 XML
- 对您的 XML 语法进行代码的颜色化显示

["点击进入下载界面:xmlspy"](http://www.pc0359.cn/downinfo/63270.html)

## <a id="xsl_transform_a">转换</a>

从xml根据xslt生成C++、C#代码。
 
```C#
#!/bin/bash
./CsTransfer.exe output/../../../C2CSTBondMMApi/STPDataType.h transfer/xml/EntityModel.xml transfer/xsl/DataType_template.xsl


namespace CsTransfer
{
    /// <summary>
    /// 从xml根据xslt生成C++、C#代码。
    /// </summary>
    class Program
    {
        static void Main(string[] args)
        {
            if (args.Length < 3)
            {
                Console.WriteLine("Error: failed to check command line arguments");
                Console.WriteLine("Usage: java -jar pump.jar <output-file> <model-file> <template-file>");
                Console.WriteLine("For <output-file>: DataStruct.h");
                Console.WriteLine("For <model-file>: EntityModel.xml");
                Console.WriteLine("For <template-file>: DataStruct_template.xsl");
                return;
            }

            // Load the style sheet.
            XslCompiledTransform xslt = new XslCompiledTransform();
            xslt.Load(args[2]);

            // Execute the transform and output the results to a file.
            xslt.Transform(args[1], args[0]);
        }
    }
}

```
用`XsltArgumentList`实现xsl的参数调用
```C#
// Load the style sheet.
XslCompiledTransform xslt = new XslCompiledTransform();
xslt.Load(args[2]);

XsltArgumentList arguments = new XsltArgumentList();
var inparams = args[5].Split(new char[] { '+' });
var inmodels = model.Split(new char[] { '+' });
for (int i=0; i< Math.Min(inparams.Length, inmodels.Length); ++i)
{
    Console.WriteLine($"Add param {inparams[i]}={inmodels[i]}.");
    arguments.AddParam(inparams[i], "", inmodels[i]);
}
using (var writer = new StreamWriter(path + inmodels[0] + args[4]))
{
    // Execute the transform and output the results to a file.
    xslt.Transform(args[1], arguments, writer);
}

//xlst文件
<?xml version="1.0" encoding="utf-8"?>
<xsl:stylesheet version="1.0" xmlns:xsl="http://www.w3.org/1999/XSL/Transform">
<xsl:output method="text" encoding="utf-8"/>
<xsl:param name="widgetName"></xsl:param>

<xsl:template match="/">
  using System.Windows;
  using System.Windows.Controls;
  
  namespace Demo
  {
    using Utils;
    /// &lt;summary&gt;
    /// <xsl:value-of select="$widgetName" />.xaml 的交互逻辑
    /// &lt;/summary&gt;
    public partial class <xsl:value-of select="$widgetName" /> : UserControl
    {
        public <xsl:value-of select="$widgetName" />()
        {
            InitializeComponent();
        }
    }
  }
</xsl:template>
</xsl:stylesheet>
```

### 总结
**今天一小步 未来一大步**
![summary.jpg](/assets/img/summary.jpg "总结")
